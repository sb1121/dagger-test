import org.gradle.plugins.ide.eclipse.model.ClasspathEntry
import org.gradle.plugins.ide.eclipse.model.SourceFolder

apply plugin: "com.diffplug.eclipse.apt"

project.eclipse.classpath.file.whenMerged {
    String path = '.apt_generated/main'
    ClasspathEntry entry = entries.find { it.path == path }
    if (entry == null) {
        String output = 'build/default'
        entry = new SourceFolder(path, output)
        entries.add(entry)
    }
    if (entry instanceof SourceFolder) {
        SourceFolder sf = (SourceFolder) entry
        Map<String, Object> attributes = sf.getEntryAttributes()
        attributes.put('ignore_optional_problems', 'true')
        attributes.put('optional', 'true')
    }
}

project.eclipse.jdt.apt {
    genSrcDir = '.apt_generated/main'
}

tasks.register('ensureDirectory') {
    doLast {
        mkdir '.apt_generated/main'
    }
}

tasks.eclipse.dependsOn('ensureDirectory')
tasks.eclipse.dependsOn('cleanEclipse')

dependencies {
    api project(':testing-api')

    implementation("com.google.dagger:dagger:2.39.1")
    annotationProcessor("com.google.dagger:dagger-compiler:2.39.1")
}
